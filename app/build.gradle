plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.hqumath.demo' //影响R类生成
    compileSdk 33

    defaultConfig {
        applicationId "com.hqumath.demo" //影响AndroidManifest中package
        minSdk 21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 33
        versionCode 20210902
        versionName "2.1"
        //multiDexEnabled true
    }
    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions{
        jvmTarget = JavaVersion.VERSION_17
    }
    //配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName
            if (variant.buildType.name.equals('release')) {
                fileName = "AndroidDemo_${variant.mergedFlavor.versionName}_${variant.mergedFlavor.versionCode}.apk"
            } else if (variant.buildType.name.equals('debug')) {
                fileName = "AndroidDemo_${variant.mergedFlavor.versionName}_debug_${variant.mergedFlavor.versionCode}.apk"
            }
            outputFileName = fileName
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.2'//多语言切换需1.3.0及以上版本
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    //rxjava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    //network
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'//返回数据转换器-Gson
    //implementation 'com.squareup.retrofit2:converter-scalars:2.4.0'//返回数据转换器-String
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'//网络请求适配器
    //权限获取
    implementation 'com.yanzhenjie:permission:2.0.3'
    //屏幕适配
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    //下拉刷新
    implementation  'io.github.scwang90:refresh-layout-kernel:3.0.0-alpha'      //核心必须依赖
    implementation  'io.github.scwang90:refresh-header-classics:3.0.0-alpha'    //经典刷新头

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1" //核心协程依赖
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2' //lifecycleScope 对于 Activity/Fragment
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2' //viewModelScope 对于 ViewModel
}
